#!/usr/bin/env escript
%% -*- erlang -*-
-mode(compile).

-import(lists, [foldl/3, filter/2, sort/1, foreach/2, sort/2]).

main(Args0) ->
    {Base,Other,OutDir} = opts(Args0),
    io:format("Comparing ~p against ~p, writing output to ~p~n",
              [Base, Other, OutDir]),
    B0 = read_qfiles(Base),
    Bc = read_cfiles(Base),
    O0 = read_qfiles(Other),

    B = add_loaded_counts(B0, Bc),
    O = add_loaded_counts(O0, read_cfiles(Other)),


    UnfilteredFuns = sets:to_list(
                       sets:intersection(sets:from_list(maps:keys(B)),
                                         sets:from_list(maps:keys(O)))),
    Funs = lists:filter(fun({beam_ssa_opt,_,_}) ->
                                false;
                           ({beam_ssa,_,_}) ->
                                false;
                           (_) ->
                                true
                        end,
                        UnfilteredFuns),
    TotalNoofFuns = length(UnfilteredFuns),
    io:format("~p functions in total~n", [TotalNoofFuns]),
    io:format("Supressed ~p functions~n",
              [length(UnfilteredFuns) - length(Funs)]),
    io:format("~p shown functions~n", [length(Funs)]),
    R = [{Fun, map_diff(maps:get(Fun, O), maps:get(Fun, B))} || Fun <- Funs],
    NonZero = filter(fun({_, Map}) -> any_value_nonzero(Map) end, R),
    [{_,Columns0}|_] = NonZero,
    Columns = sort(maps:keys(Columns0)),
    io:format("~p~n", [Columns]),
    foreach(fun(Col) ->
                    Sorted = sort_by_column(NonZero, Col),
                    Hist = histogram(NonZero, Col),
                    Filename =
                        filename:join(OutDir, atom_to_list(Col) ++ ".html"),
                    ok = filelib:ensure_dir(Filename),
                    to_html(Columns, Filename, Sorted, Hist, TotalNoofFuns)
            end, Columns),
    ok.

sort_by_column(Rows, Col) ->
    sort(fun(A={_,Map0},B={_,Map1}) ->
                 case {maps:get(Col, Map0, false),
                       maps:get(Col, Map1, false)} of
                     {X,X} -> A < B;
                     {X,Y} -> X < Y
                 end
         end,
         Rows).

to_html(Columns, Filename, Rows, Hist, TotalNoofFuns) ->
    Out = ["<html>\n",
           format_histogram(Hist, TotalNoofFuns),
           "<table style=\"width:100%\">\n",
           %% The header
           "<tr><th>fun</th>",
           [["<th style=\"text-align:right\">", atom_to_list(C), "</th>"]
            || C <- Columns], "</tr>\n",
           %% The functions
           [["<tr><td>", io_lib:format("~p:~p/~p", [M,F,A]), "</td>",
             [["<td style=\"text-align:right\">",
               case maps:get(C, Data, false) of
                   false -> "n/a";
                   V -> integer_to_list(V)
               end, "</td>"]
              || C <- Columns], "</tr>\n"]
            || {{M,F,A}, Data} <- Rows],
           "</table></html>\n"],
    ok = file:write_file(Filename, Out).

usage() ->
    S = ["usage: qreport BASE_DIRECTORY OTHER_DIRECTORY OUTPUT_DIRECTORY\n\n"
         "Options:\n"
         "\n"
         "DESCRIPTION\n"
         "\n"
         "Compare the quality report files in two directories against each"
         " other\n"
        ],
    io:put_chars(S),
    halt(1).

opts([Base,Other,Out]) ->
    {Base,Other,Out};
opts(_) ->
    usage().

map_diff(A, B) ->
    maps:map(fun(Key, Val) -> Val - maps:get(Key, B) end, A).

any_value_nonzero(Map) ->
    any_value_nonzero1(maps:values(Map)).
any_value_nonzero1([]) ->
    false;
any_value_nonzero1([0|Rest]) ->
    any_value_nonzero1(Rest);
any_value_nonzero1(_) ->
    true.

read_qfiles(Dir) ->
    read_pair_files(Dir, "*.Q").

read_cfiles(Dir) ->
    read_pair_files(Dir, "*.counts").

read_pair_files(Dir, Wildcard) ->
    foldl(fun(F, Data) ->
                  {ok,[D]} = file:consult(F),
                  maps:merge(maps:from_list(D), Data)
          end,
          #{},
          filelib:wildcard(filename:join(Dir, Wildcard))).

histogram(Rows, Col) ->
    H = foldl(fun({_Fun, Values}, Hist) ->
                      N = maps:get(Col, Values, 'n/a'),
                      Old = maps:get(N, Hist, 0),
                      Hist#{N => Old + 1}
              end, #{}, Rows),
    {Col,sort(maps:to_list(H))}.

pc(Part, Total) ->
    io_lib:format("~.1f", [100.0*Part/Total]).

format_histogram({Key, Ls}, TotalNoofFuns) ->
    {Total,Improved,NoChange,Worse} = foldl(fun({Delta,N}, {T,I,NC,W}) ->
                                                    S = T + N,
                                                    if Delta =:= 'n/a' ->
                                                            {S,I,NC,W};
                                                       Delta == 0 ->
                                                            {S,I,NC + N,W};
                                                       Delta < 0 ->
                                                            {S,I + N,NC,W};
                                                       Delta > 0 ->
                                                            {S,I,NC,W + N}
                                                    end
                                            end, {0,0,0,0}, Ls),
    ["<table>\n",
     "<tr><th>Classification</th><th>Count</th><th>%<br>changed</th><th>%<br>all</th></tr>\n",
     "<tr><td>Better</td><td style=\"text-align:right\">",
     integer_to_list(Improved), "</td><td style=\"text-align:right\">",
     pc(Improved,Total), "</td><td style=\"text-align:right\">",
     pc(Improved,TotalNoofFuns), "</td></tr>\n",
     "<tr><td>N/C</td><td style=\"text-align:right\">",
     integer_to_list(NoChange), "</td><td style=\"text-align:right\">",
     pc(NoChange,Total), "</td><td style=\"text-align:right\">",
     pc(Improved,TotalNoofFuns), "</td></tr>\n",
     "<tr><td>Worse</td><td style=\"text-align:right\">",
     integer_to_list(Worse), "</td><td style=\"text-align:right\">",
     pc(Worse,Total), "</td><td style=\"text-align:right\">",
     pc(Improved,TotalNoofFuns), "</td></tr>\n",
     "</table>\n",

     "<table>\n",
     "<tr><th>", atom_to_list(Key), " change</th>",
     "<th>function count</th><th>%</th>",
     [["<tr><td style=\"text-align:right\">",
       case Delta of
           'n/a' -> "n/a";
            _ -> integer_to_list(Delta)
       end,
       "</td>",
       "<td style=\"text-align:right\">", integer_to_list(Count), "</td>",
       "<td style=\"text-align:right\">", pc(Count, Total), "</td>",
       "</tr>"] || {Delta,Count} <- Ls],
     "</table>\n"].

add_loaded_counts(Info, Counts) ->
    maps:map(fun(MFA, Map) ->
                     case Counts of
                         #{ MFA := Count } ->
                             Map#{loaded_instructions=>Count};
                         _ ->
                             Map
                     end
             end, Info).
